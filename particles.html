<!--Referenced https://happycoding.io/tutorials/processing/processing-js-->
<!--This won't run without proper html-head-body structure-->
<!DOCTYPE html>
<html>
	<head>
		<title>Simplified Particle Simulator</title>
		<script src="https://cdnjs.cloudflare.com/ajax/libs/processing.js/1.6.0/processing.min.js"></script>
	</head>
	<!--start processing and HTML bit-->
	<body>
		<h1>Simplified Particle Simulator</h1>
		<h2>By Kevin Mc.</h2>
		<script type="application/processing">
			//This is the web version. IT MAY NOT BE UP-TO-DATE.
		
			// Important note: this program assumes that the positions of particles are always fully known, which is not accurate. There are many other
			// approximations that I'm making because I don't want writing this program to become my job for multiple years.

			// Yes, I know I skipped access modifiers here
			// That's not the only bodged part of this code
			// This whole thing is an unorganized bodge that would probably take a whole day of refactoring if I wanted to expand it much more

			// TODO: switch to actual units (not "pixels", "charge units"), clean up code, make field shader (maybe...),
			// calculate electric field only once per particle per frame(?), make sure animation works well with FPS, bring configuration to top, various polish.
			// auto text wrapping, leave recently de-selected particle on top of rendering order
			// Feature TODO: toggle add/remove particles, gravity, about popup, electric field lines(?), force to keep particles on screen,
			// complete world looping (non-bodged selection rendering, charge wrapping, mouse selection, particle path traces

			import java.util.List;

			//Configuration
			boolean debug = true;
			float animationMultiplier = 0.5f;
			int velocitySteps = 12;
			float forceScale = 1024.0f;
			float defaultRadius = 9.0f;
			float fakeFriction = 0.001f * 0.0f;

			//Actual code
			List<Particle> particles = new ArrayList<Particle>();
			List<ToggleButton> toggles = new ArrayList<ToggleButton>();

			Particle selectedParticle = null;
			boolean popupShown = !debug;

			Particle positiveSample = new Particle(300, 480, 1.0f);
			Particle negativeSample = new Particle(380, 480, -1.0f);

			ToggleButton fieldToggle = new ToggleButton(20, 420, 20, 20, "Visualize Electric Field");
			ToggleButton forceToggle = new ToggleButton(20, 450, 20, 20, "Visualize Forces");
			ToggleButton chargeToggle = new ToggleButton(20, 480, 20, 20, "Visualize Charges");
			ToggleButton animationToggle = new ToggleButton(20, 510, 20, 20, "Animate Particles");
			ToggleButton loopToggle = new ToggleButton(20, 540, 20, 20, "Loop Simulation Area");

			void setup() {
			  size(600, 600);
			  displayDensity = 200;
			  frameRate(30);
			  //textFont(createFont("monospace"));

			  for (int i = 0; i < 5; i++) {
				particles.add(new Particle(random(600), random(400), i % 2 == 0 ? 1.0f : -1.0f));
			  }

			  positiveSample.radius = 12.0f;
			  negativeSample.radius = 12.0f;

			  toggles.add(fieldToggle);
			  toggles.add(forceToggle);
			  toggles.add(chargeToggle);
			  toggles.add(animationToggle);
			  toggles.add(loopToggle);
			}

			void draw() {
			  drawBoard();
			  drawGUI();
			  if (popupShown) {   //This whole section is a mess, should move to GUI, probably to own function
				stroke(0);
				fill(192);
				rect(-1, 50, 602, 550);
				fill(128);
				rect(560, 60, 30, 30, 5);
				textAlign(CENTER, CENTER);
				textSize(30);
				fill(255);
				text("x", 575, 75);

				textAlign(CENTER, TOP);
				fill(0);
				text("About This Simulation", 300, 60);
				textSize(15);
				textAlign(LEFT, TOP);
				text("This is a very fancy simulation. Really. Just take my word for it.\n(I'll fill in some text later, maybe).", 20, 120);
			  }
			}

			void mousePressed() {
			  if (popupShown) {
				popupShown ^= (mouseX >= 560 && mouseY >= 60 && mouseX <= 590 && mouseY <= 90);
				return;
			  }

			  for (Particle particle : particles) {
				if (distance(particle.x, particle.y, mouseX, mouseY) < particle.radius) {
				  selectedParticle = particle;
				  break;
				}
			  }

			  for (ToggleButton toggle : toggles) {
				if (mouseX >= toggle.x && mouseX <= toggle.x + toggle.sizeX && mouseY >= toggle.y && mouseY <= toggle.y + toggle.sizeY) {
				  toggle.on ^= true;
				  break;
				}
			  }

			  if (mouseX >= 280 && mouseX <= 320 && mouseY >=  460 && mouseY <= 500) {
				selectedParticle = new Particle(positiveSample.x, positiveSample.y, positiveSample.charge);
				particles.add(selectedParticle);
			  } else if (mouseX >= 360 && mouseX <= 400 && mouseY >=  460 && mouseY <= 500) {
				selectedParticle = new Particle(negativeSample.x, negativeSample.y, negativeSample.charge);
				particles.add(selectedParticle);
			  }
			}

			void mouseReleased() {
			  if (selectedParticle != null ? selectedParticle.x >= 440 && selectedParticle.y >= 460 && selectedParticle.x <= 480 && selectedParticle.y <= 500 : false) {
				particles.remove(selectedParticle);
			  }

			  selectedParticle = null;
			}

			void drawBoard() {
			  background(128, 128, random(1) > 0.01 ? 132 : 124);

			  if (animationToggle.on) {
				for (int i = 0; i < velocitySteps; i++) {
				  for (Particle particle : particles) {
					//Should this be divided by velocitySteps?
					PVector force = getElectricField(particle.x, particle.y).mult(particle.charge);
					particle.vx += force.x * animationMultiplier;
					particle.vy += force.y * animationMultiplier;

					particle.vx *= (1.0f - (fakeFriction / velocitySteps));
					particle.vy *= (1.0f - (fakeFriction / velocitySteps));
				  }

				  for (Particle particle : particles) {
					particle.x += particle.vx / (float) velocitySteps * animationMultiplier;
					particle.y += particle.vy / (float) velocitySteps * animationMultiplier;
				  }
				}
			  }

			  if (fieldToggle.on) {
				noStroke();
				int cellSize = 5;
				for (int x = 0; x < 600 / cellSize; x++) {
				  for (int y = 0; y < 400 / cellSize; y++) {
					PVector charge = getElectricField(x * cellSize + (cellSize / 2), y * cellSize + (cellSize / 2));

					//float displayCharge = charge * 16.0f; //So far, best function for contrast
					//fill(255.0f * (displayCharge * 0.5f + 0.5f));

					float chargeScale = 16.0f;
					fill(255.0f * (chargeScale * charge.x * 0.5f + 0.5f), 255.0f * (chargeScale * charge.y * 0.5f + 0.5f), 0.0f);

					rect(x * cellSize, y * cellSize, cellSize, cellSize);
				  }
				}
			  }

			  if (loopToggle.on) {
				for (Particle particle : particles) {
				  if (particle.x < 0) {
					particle.x += width;
				  }

				  if (particle.y < 0) {
					particle.y += 400;
				  }

				  particle.x %= width;
				  particle.y %= 400;
				}
			  }

			  if (selectedParticle != null) {
				selectedParticle.x = mouseX;
				selectedParticle.y = mouseY;
			  }

			  for (Particle particle : particles) {    
				particle.draw(); //some drawing, not all

				noFill();
				stroke(0);
				//ellipse(particle.x, particle.y, particle.radius + 8, particle.radius + 8);
			  }

			  if (forceToggle.on) {
				stroke(0, 0, 64);
				strokeWeight(3);

				for (Particle particle : particles) {
				  PVector force = getElectricField(particle.x, particle.y).mult(particle.charge);
				  line(particle.x, particle.y, particle.x + force.x * forceScale, particle.y + force.y * forceScale);
				}

				strokeWeight(1);
			  }
			}

			void drawGUI() {
			  noStroke();
			  fill(192);
			  rect(0, 400, width, 200);

			  stroke(0);
			  fill(192);
			  rect(280, 460, 40, 40);
			  rect(360, 460, 40, 40);
			  rect(440, 460, 40, 40);
			  positiveSample.draw();
			  negativeSample.draw();

			  fill(255);
			  textSize(50);
			  textAlign(CENTER, CENTER);
			  text("x", 460, 475);

			  textSize(16);
			  textAlign(LEFT, CENTER);
			  for (ToggleButton toggle : toggles) {
				noFill();
				stroke(0);
				rect(toggle.x, toggle.y, toggle.sizeX, toggle.sizeY);

				fill(0);
				if (toggle.on) {
				  rect(toggle.x + 5, toggle.y + 5, toggle.sizeX - 10, toggle.sizeY - 10);  //Yes, janky
				}

				text(toggle.label, toggle.x + 25.0f, toggle.y + 10.0f);  //Add (5, -2.5) to CENTER of text
			  }

			  if (selectedParticle != null) {
				selectedParticle.draw();
			  }
			}

			class Particle {
			  public float x, y, vx, vy;
			  public float radius; //visual
			  public float charge;

			  public Particle(float x, float y, float charge) {
				this.x = x;
				this.y = y;
				this.vx = 0.0f;
				this.vy = 0.0f;
				this.radius = defaultRadius;
				this.charge = charge;
			  }

			  public void draw() {
				fill(chargeToggle.on ? charge > 0 ? color(0, 0, 255) : color(255, 0, 0) : color(255));  //Just as bad as messy if tower
				stroke(0);
				ellipse(x, y, radius * 2.0f, radius * 2.0f);

				// Try adding if statements or something, see if it's faster; these if conditions are janky but will accept for now
				if (loopToggle.on && selectedParticle != this && animationToggle.on && radius == defaultRadius) {
				  ellipse(x + 600, y, radius * 2.0f, radius * 2.0f);
				  ellipse(x + 600, y + 400, radius * 2.0f, radius * 2.0f);
				  ellipse(x, y + 400, radius * 2.0f, radius * 2.0f);
				  ellipse(x - 600, y + 400, radius * 2.0f, radius * 2.0f);
				  ellipse(x - 600, y, radius * 2.0f, radius * 2.0f);
				  ellipse(x - 600, y - 400, radius * 2.0f, radius * 2.0f);
				  ellipse(x, y - 400, radius * 2.0f, radius * 2.0f);
				  ellipse(x + 600, y - 400, radius * 2.0f, radius * 2.0f);
				}
			  }
			}

			class ToggleButton {
			  public int x, y, sizeX, sizeY;
			  public boolean on;  // Yes, this is a silly name, but it's short --> thingy.active vs thingy.on
			  public String label;

			  public ToggleButton(int x, int y, int sizeX, int sizeY, String label) {
				this.x = x;
				this.y = y;
				this.sizeX = sizeX;
				this.sizeY = sizeY;
				this.label = label;

				on = false;
			  }
			}

			float distance(float x1, float y1, float x2, float y2) {
			  return (float) Math.sqrt(Math.pow(x1 - x2, 2) + Math.pow(y1 - y2, 2));
			}

			PVector getElectricField(float x, float y) {
			  PVector charge = new PVector(0.0f, 0.0f);
			  for (Particle particle : particles) {
				float strength = particle.charge / distance(x, y, particle.x, particle.y);

				if (x == particle.x && y == particle.y) {
				  continue;
				}

				PVector direction = new PVector(x - particle.x, y - particle.y);
				direction.normalize(); //Split from previous line for web compatibility
				direction.mult(strength);
				charge.add(direction);
			  }
			  return charge;
			}
		</script>
		<canvas> </canvas>
		<p>I am aware that this looks pixelated on some displays. Fixing that would take about an hour, which I don't currently have.</p>
	</body>
	<footer>
		<style>
			html {
				color: white;
				background-color: darkgray;
				text-align: center;
			}
			canvas {
				margin-left: calc(50% - 300px);
				margin-right: calc(50% - 300px);
				outline-style: solid;
				outline-width: medium;
				outline-color: white;
			}
			p {
				font-size: 50%;
				opacity: 50%;
			}
		</style>
	</footer>
</html>